plugins {
    id 'war'
    id 'application'
    id "io.franzbecker.gradle-lombok" version "1.14"
    id "com.devsoap.plugin.vaadin"  version "1.3.1" apply false
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
        maven {
            url "http://maven.restlet.org"
        }
        maven {
            name "vaadin-addons"
            url "http://maven.vaadin.com/vaadin-addons"
        }
        maven {
            name "sonatype vaadin"
            url "https://oss.sonatype.org/content/repositories/vaadin/"
        }
        maven {
            url 'http://jaspersoft.artifactoryonline.com/jaspersoft/third-party-ce-artifacts/'
        }
        maven {
            url 'http://jasperreports.sourceforge.net/maven2'
        }
        flatDir(dirs: file(project(":constellio").projectDir.getAbsolutePath() + '/libs/'))
    }

    sourceCompatibility = 8
    targetCompatibility = 8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.fork = true
        options.incremental = true
    }

    tasks.withType(Javadoc) {
        task ->
            enabled = false;
            options.encoding = 'UTF-8'
    }

    jacocoTestReport {
        group = "reporting"
        reports {
            xml.enabled false
            html.enabled true
            csv.enabled false
        }
    }

    test {
        jacoco {
            destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        }
    }

    task wrapper(type: Wrapper) {
        gradleVersion = '3.4'
    }

    String projectVersion = "4.2.42"
    if (new File("./version").exists()) {
        projectVersion = new File("./version").text.trim()
    }

    project.ext.set("VERSION", projectVersion)
}
